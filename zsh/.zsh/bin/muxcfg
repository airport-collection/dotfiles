#!/usr/bin/env zsh

function muxcfg::new-workdir-cmd() {
  if [[ -n ${__GOOGLE__+1} ]]; then
    echo "muxcfg::git5wrapper"
  else
    echo "git::new-workdir"
  fi
}
function muxcfg::usage() {
  cat <<EOF
Usage: muxcfg [arguments] [command] [project]

Arguments:
  -h or --help          Print the usage information
  -s or --src           Source git repo directory
  -t or --dst           Destination git repo directory
  -y or --noconfirm     Skip confirmation
  -v or --verbose       Verbose
  -n or --dryrun        Dryrun

Commands:
  start                 Create project repository, branch and config, start tmux session
  delete                Delete project repository, branch and config, kill tmux session
EOF
  exit 1
}
function muxcfg::start() {
  setopt localoptions err_return

  zparseopts -D -K -M -E -- s:=src t:=dst -src:=src -dst:=dst
  local _proj _cwd _src _dst _muxcfg _tmpcfg
  _proj="$1"
  [[ ${#src} -ge 2 ]] && _src="${src[2]}" || io::err "Missing required option -s or --src"
  [[ ${#dst} -ge 2 ]] && _dst="${dst[2]}/${_proj}" || io::err "Missing required option -t or --dst"
  _cwd="$(muxcfg::getcwd ${_dst} ${_proj})"

  local _muxcfg="$MUXCFGDIR/${_proj}.yml"
  if [[ -e "$_muxcfg" && ! -n ${opts[--noconfirm]+1} ]]; then
    if io::yes_or_no "$_muxcfg exists, overwrite"; then
      io::warn "$_muxcfg will be overwritten."
      _tmpcfg=$(mktemp)
      shell::eval "sed -e \"s#\\\${SESSION}#${_proj}#\" -e \"s#\\\${ROOT}#${_cwd}#\" $MUXCFGDIR/project.yml.template > ${_tmpcfg}"
      shell::eval "vimdiff $MUXCFGDIR/${_proj}.yml ${_tmpcfg}"
      io::yes_or_no "Continue"
      shell::eval "mv ${_tmpcfg} $MUXCFGDIR/${_proj}.yml"
    else
      return 1
    fi
  else
    shell::eval "sed -e \"s#\\\${SESSION}#${_proj}#\" -e \"s#\\\${ROOT}#${_cwd}#\" $MUXCFGDIR/project.yml.template > ${_muxcfg}"
  fi

  if [[ -d "${_dst}" ]]; then
    io::hl "[muxcfg] Directory ${_dst} exists."
    io::yes_or_no "Skip branching and start with existing copy"
  else
    shell::eval "whence -f $(muxcfg::new-workdir-cmd) > /dev/null"
    shell::eval "$(muxcfg::new-workdir-cmd) ${_src} ${_dst} ${_proj}"
  fi
  shell::eval "mux start ${_proj}"
}
function muxcfg::delete() {
  setopt localoptions err_return

  zparseopts -D -K -M -E -- s:=src t:=dst -src:=src -dst:=dst
  local _proj _cwd _src _dst _muxcfg
  _proj="$1"
  [[ ${#src} -ge 2 ]] && _src="${src[2]}" || io::err "Missing required option -s or --src"
  [[ ${#dst} -ge 2 ]] && _dst="${dst[2]}/${_proj}" || io::err "Missing required option -t or --dst"
  _cwd="$(muxcfg::getcwd ${_dst} ${_proj})"

  if [[ ! -n ${opts[--noconfirm]+1} ]]; then
    io::yes_or_no "Delete project ${_proj}"
  fi

  if [[ -e "$MUXCFGDIR/${_proj}.yml" ]]; then
    io::vlog 1 "Deleting tmuxinator config $MUXCFGDIR/${_proj}.yml"
    shell::eval "mux delete ${_proj}"
    cd "${_src}" || return 1
    io::vlog 1 "Deleting branch ${_proj}"
    shell::eval "git branch -D ${_proj}"
    io::vlog 1 "Deleting directory ${_dst}"
    shell::eval "rm -rf ${_dst}"
    io::vlog 1 "Killing tmux session ${_proj}"
    shell::eval "tmux kill-session -t ${_proj}"
  else
    io::err "No project named ${_proj} exists."
  fi
}
function muxcfg::git5wrapper() {
  setopt localoptions err_return
  shell::eval "git5 mirror $1 $2 --allow_existing_branch --branch=$3"
}
function muxcfg::main() {
  setopt localoptions err_return
  zparseopts -D -K -M -E -- h=help n=dryrun v=verbose s:=src t:=dst \
    -help=help -dryrun=dryrun -verbose=verbose -src:=src -dst:=dst

  [[ -n ${help} ]] && muxcfg::usage
  [[ -n ${dryrun} ]] && mode::set-dryrun
  [[ -n ${verbose} ]] && setopt localoptions verbose
  [[ ${#src} -lt 2 ]] && io::err "Missing required option -s or --src"
  [[ ${#dst} -lt 2 ]] && io::err "Missing required option -t or --dst"

  shell::eval "whence tmuxinator > /dev/null"
  shell::eval "mkdir -p "$MUXCFGDIR""

  local _subcmd _git_new_workdir_cmd
  _subcmd="$1"
  shift

  io::vlog 1 "[muxcfg] Subcommand: ${src[2]}"
  case "${_subcmd}" in
    start|delete )
      "muxcfg::${_subcmd}" -s "${src[2]}" -t "${dst[2]}" "$@"
      ;;
    * )
      io::hl "Unknown subcommand ${_subcmd}."
      muxcfg::usage
      ;;
  esac
  return 0
}
function muxcfg::getcwd() {
  if [[ -n ${__GOOGLE__+1} ]]; then
    echo "${1}/${2}/google3"
  else
    echo "${1}/${2}"
  fi
}

local _config_dir
_config_dir="${0:A:h}/../../"
fpath=(${_config_dir:a}/.zsh/lib ${_config_dir:a}/.zsh/functions $fpath)
autoload -Uz -- ${_config_dir:a}/.zsh/lib/[^_]*(:t)
autoload -Uz -- ${_config_dir:a}/.zsh/functions/[^_]*(:t)

setopt LOCAL_OPTIONS
MUXCFGDIR="$HOME/.tmuxinator"
muxcfg::main "$@"

# vim: filetype=zsh sw=2 ts=2 sts=2 et tw=80 foldlevel=0 nospell

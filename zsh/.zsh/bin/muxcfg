#!/usr/bin/env zsh
#
source "$HOME/.zsh/lib/base.zsh"
source "$HOME/.zsh/lib/git.zsh"
source "$HOME/.zsh/lib/io.zsh"
source "$HOME/.zsh/lib/mode.zsh"
source "$HOME/.zsh/lib/shell.zsh"

function muxcfg::start() {
  setopt localoptions unset err_return
  local -A _fn_options
  base::getopt noconfirm,src:,dst:,cwd:,cmd:,proj: "$@"
  local _proj _cwd _cmd _src _dst _muxcfg
  [[ -z "${_fn_options[--proj]}" ]] || _proj="${_fn_options[--proj]}"
  [[ -z "${_fn_options[--src]}" ]] || _src="${_fn_options[--src]}"
  [[ -z "${_fn_options[--dst]}" ]] || _dst="${_fn_options[--dst]}"
  [[ -z "${_fn_options[--cwd]}" ]] || _cwd="${_fn_options[--cwd]}"
  [[ -z "${_fn_options[--cmd]}" ]] || _cmd="${_fn_options[--cmd]}"

  local _muxcfg="$MUXCFGDIR/${_proj}.yml"
  if [[ -e "$_muxcfg" && \
    (-z ${_fn_options[--noconfirm]} && -z ${_fn_options[-y]}) ]]; then
    if io::yes_or_no "$_muxcfg exists, overwrite"; then
      io::warn "$_muxcfg will be overwritten."
    else
      return 1
    fi
  fi

  if [[ -d "${_dst}" ]]; then
    io::hl "[muxcfg]: Directory ${_dst} exists!"
    mode::dryrun || return 1
  fi
  shell::eval "sed -e s#\${SESSION}#${_proj}# -e s#\${ROOT}#${_cwd}# $MUXCFGDIR/project.yml.template > ${_muxcfg}"
  shell::eval "${_cmd} ${_src} ${_dst} ${_proj}"
  shell::eval "mux start ${_proj}"
}
function muxcfg::delete() {
  setopt localoptions unset err_return
  local -A _fn_options
  base::getopt noconfirm,src:,dst:,cwd:,cmd:,proj: "$@"
  local _proj _cwd _cmd _src _dst _muxcfg
  [[ -z "${_fn_options[--proj]}" ]] || _proj="${_fn_options[--proj]}"
  [[ -z "${_fn_options[--src]}" ]] || _src="${_fn_options[--src]}"
  [[ -z "${_fn_options[--dst]}" ]] || _dst="${_fn_options[--dst]}"
  [[ -z "${_fn_options[--cwd]}" ]] || _cwd="${_fn_options[--cwd]}"
  [[ -z "${_fn_options[--cmd]}" ]] || _cmd="${_fn_options[--cmd]}"

  if [[ (-z ${_fn_options[--noconfirm]} && -z ${_fn_options[-y]}) ]]; then
    io::yes_or_no "Delete project ${_proj}"
  fi

  if [[ -e "$MUXCFGDIR/${_proj}.yml" ]]; then
    io::vlog 1 "Deleting tmuxinator config $MUXCFGDIR/${_proj}.yml"
    shell::eval "mux delete ${_proj}"
    cd "${_src}" || return 1
    io::vlog 1 "Deleting branch ${_proj}"
    shell::eval "git branch -D ${_proj}"
    io::vlog 1 "Deleting directory ${_dst}"
    shell::eval "rm -rf ${_dst}"
    io::vlog 1 "Killing tmux session ${_proj}"
    shell::eval "tmux kill-session -t ${_proj}"
  else
    io::err "No project named ${_proj} exists."
  fi
}
function muxcfg::git5wrapper() {
  setopt localoptions err_return
  local -A _fn_options
  _fn_options=(-src '' -dst '' -branch '')
  local -a _fn_args
  _fn_args=("${(@M)@:#-*}")
  base::parseargs

  for arg in ${(k)_fn_options}; do
    [[ -z ${_fn_options[${arg}]} ]] && io::err "Missing required argument ${arg}"
  done
  shell::eval "git5 mirror ${_fn_options[-src]} ${_fn_options[-dst]} --allow_existing_branch --branch=${_fn_options[-branch]}"
}
function muxcfg::usage() {
  cat <<EOF
Usage: muxcfg [arguments] [command] [project]

Arguments:
  --help          Print the usage information
  --src           Source git repo directory
  --dst           Destination git repo directory
  --noconfirm     Skip confirmation

Commands:
  start                 Create project repository, branch and config, start tmux session
  delete                Delete project repository, branch and config, kill tmux session
EOF
  exit 1
}
function muxcfg::main() {
  setopt localoptions unset err_return
  local -A _fn_options
  local -a _fn_args
  whence tmuxinator > /dev/null
  shell::eval "mkdir -p \"$MUXCFGDIR\""
  base::getopt help,noconfirm,src:,dst: "$@"
  io::vlog 2 "${(kv)_fn_options}"
  io::vlog 2 "${_fn_args}"

  [[ ! -z "${_fn_options[--help]}") ]] && muxcfg::usage

  eval set -- "${_fn_args}"
  local _subcmd _project _git_new_workdir_cmd _cwd
  _subcmd="$1"
  _project="$2"

  if [[ -e "$HOME/.google" ]]; then
    _git_new_workdir_cmd="muxcfg::git5wrapper"
    _cwd=${_fn_options[--dst]}/${_project}/google3
  else
    _git_new_workdir_cmd="git::new_workdir"
    _cwd=${_fn_options[--dst]}/${_project}
  fi
  whence -f "${_git_new_workdir_cmd}" > /dev/null

  io::vlog 1 "[muxcfg] Subcommand: ${_subcmd}"
  case "${_subcmd}" in
    start|delete )
      if [[ ! -z ${_fn_options[--noconfirm]} ]]; then
        "muxcfg::${_subcmd}" --src "${_fn_options[--src]}" --dst "${_fn_options[--dst]}" --proj "${_project}" --cmd "${_git_new_workdir_cmd}" --cwd "${_cwd}" -y
      else
        "muxcfg::${_subcmd}" --src "${_fn_options[--src]}" --dst "${_fn_options[--dst]}" --proj "${_project}" --cmd "${_git_new_workdir_cmd}" --cwd "${_cwd}"
      fi
      ;;
    * )
      muxcfg::usage
      ;;
  esac
  return 0
}

MUXCFGDIR="$HOME/.tmuxinator"
muxcfg::main "$@"

# vim: filetype=zsh sw=2 ts=2 sts=2 et tw=80 foldlevel=0 nospell

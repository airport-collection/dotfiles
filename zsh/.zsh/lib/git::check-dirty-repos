# vim: filetype=zsh sw=2 ts=2 sts=2 et tw=80 foldlevel=0 nospell

: <<=cut
=item Function C<git::check-dirty-repos>

Check subdirs of current directory, and report repos that are dirty
Number of arguments accepted: --exclude-detached

@return NULLPTR
=cut

setopt localoptions err_return nounset

zparseopts -D -A opts -exclude-detached -exclude-ahead

[[ -n ${opts[--exclude-detached]+1} ]] && \
  io::msg "Considering detached as dirty."
local -a dirty_repos
dirty_repos=()
for dir in */; do
  io::vlog 2 "[${0:t}] Checking ${dir}"
  pushd "${dir}"
  if [[ -d '.git' ]]; then
    if [[ -n $(git status --porcelain) ]]; then
      # Dirty
      dirty_repos+=${dir}
    elif [[ -z ${opts[--exclude-ahead]+1} && ($(git diff origin/master | wc -l) -ne 0) ]]; then
      # Check ahead
      dirty_repos+=${dir}
    elif [[ -z ${opts[--exclude-detached]+1} && ("$(git status)" =~ '^HEAD detached.*') ]]; then
      # Check detached
      dirty_repos+=${dir}
    fi
  fi
  popd
done

if [[ ${#dirty_repos} -gt 0 ]]; then
  io::hl "${#dirty_repos} dirty repo(s) found!"
  for dir in ${dirty_repos}; do
    io::hl "Dirty repo: ${dir}"
  done
  return 1
else
  io::msg "All clean!"
  return 0
fi

# vim: filetype=zsh sw=2 ts=2 sts=2 et tw=80 foldlevel=0 nospell

setopt localoptions err_return

local -a directory reloadall
zparseopts -D -K -M -E -- d:=directory -dir:=directory \
  a=reloadall -reloadall=reloadall

local -a _dir
if [[ ${#directory} -gt 0 ]]; then
  [[ -d ${directory[2]} ]] || \
    echo "$0: ${directory[2]} is not a directory"
  _dir=("${directory[2]}")
else
  [[ ${#ZSH_LIB_DIR} -eq 0 ]] && \
    io::err "$0: ZSH_LIB_DIR not set and -d not specified."
  _dir=(${ZSH_LIB_DIR})
fi

if [[ ${#reloadall} -gt 0 ]]; then
  local -a _funcs
  _funcs=()
  for dir in ${_dir}; do
    _funcs+=(${dir}/[^_]*(:t))
  done
  set -- ${_funcs}
fi

local _mtime
for func in "$@"; do
  if (( ${+FN_REGISTRY[${func}]} )); then
    io::vlog 1 "Reload function ${func}."
    unfunction -- "${func}"
    if [[ "${func}" == "time::getmtime" ]]; then
      autoload -Uz time::getmtime
    fi
  fi
  for dir in ${_dir}; do
    if [[ -e ${dir}/${func} ]]; then
      _mtime=$(time::getmtime ${dir}/${func})
      autoload -Uz ${func}
      FN_REGISTRY[${func}]="${_mtime}"
    fi
  done
done

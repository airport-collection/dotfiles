# vim: filetype=zsh sw=2 ts=2 sts=2 et tw=80 foldlevel=0 nospell

setopt localoptions err_return

local -a directory
zparseopts -D -K -M -E -- d:=directory -dir:=directory

if [[ -z ${directory+1} ]]; then
  io::err "Missing required option -d / --dir"
fi

[[ ${#directory} -ge 2 && -d ${directory[2]} ]] || io::err "$0: ${directory[2]} is not a directory"

local _dir
_dir=${directory[2]}

if [[ $# -eq 0 ]]; then
  set -- ${_dir}/[^_]*(:t)
fi

local _mtime
for func in "$@"; do
  io::vlog 1 "Reloading function ${func}."
  _mtime=$(time::getmtime ${_dir}/${func})
  if (( ${+FN_REGISTRY[${func}]} )); then
    if [[ "$FN_REGISTRY[${func}]" != "${_mtime}" ]]; then
      io::vlog 1 "Function ${func} reloaded."
      unfunction -- "${func}"
      if [[ "${func}" == "zsh::autoload" ]]; then
        autoload -Uz "${func}"
      else
        zsh::autoload -d ${_dir} "${func}"
      fi
    fi
  else
    zsh::autoload -d "${_dir}" "${func}"
  fi
done

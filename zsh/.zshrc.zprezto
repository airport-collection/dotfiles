#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

export __SHLIB__=$HOME/Configs/shlib

# Customize to your needs...
setopt inc_append_history
setopt share_history
# Do not complete alias before completion is finished
# This would allow completion to work for aliases
# e.g. alias tmux=tmux -2
setopt complete_aliases
# Default on, resulting in a carriage return ^M when enter on the numeric pad is pressed.
# setopt no_prompt_cr

bindkey "^[OH" beginning-of-line
bindkey "^[OF" end-of-line
bindkey "^[[3~" delete-char
# bindkey "^I" complete-word
bindkey "^I" expand-or-complete-prefix
# By default <C-S> is bind to self-insert, which presents vim from getting the combination.
bindkey -r "^S"

# Let zsh history search use regex pattern
bindkey '^R' history-incremental-pattern-search-backward

source "$__SHLIB__/common.sh"

# Put the cursor at the end of line when using Up / Down for command history
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# Setup powerline for zsh prompt
function powerline_precmd() {
  export PS1="$(powerline-shell.py --colorize-hostname $? --shell zsh 2> /dev/null)"
}

function install_powerline_precmd() {
  for s in "${precmd_functions[@]}"; do
    if [ "$s" = "powerline_precmd" ]; then
      return
    fi
  done
  precmd_functions+=(powerline_precmd)
}

install_powerline_precmd

if gls --color -d . &>/dev/null 2>&1
then
  alias ls='gls --color=tty'
elif ls --color -d . &>/dev/null 2>&1
then
  alias ls='ls --color=tty'
else
  alias ls='ls -G'
fi

fpath=($HOME/.zsh-completions/src $fpath)
autoload -U compinit
compinit

# Additional configurations to load {{{

# Tmuxinator
if [ -e "$HOME/.tmuxinator.zsh" ]; then
  source ~/.tmuxinator.zsh
fi

function config_darwin() {
  alias updatedb="sudo /usr/libexec/locate.updatedb"
}

function config_linux() {
  :
}

[[ "$OSTYPE" == "darwin"* ]] && config_darwin
[[ "$OSTYPE" == "linux-gnu"* ]] && config_linux

# }}}
